# Options
# If set, parameter expansion, command substitution and arithmetic expansion are performed in prompts.
setopt PROMPT_SUBST
# Allow comments even in interactive shells.
setopt INTERACTIVE_COMMENTS

# History
setopt HIST_IGNORE_ALL_DUPS # do not put duplicated command into history list
setopt HIST_SAVE_NO_DUPS # do not save duplicated command
setopt HIST_REDUCE_BLANKS # remove unnecessary blanks
setopt INC_APPEND_HISTORY_TIME # append command to history file immediately after execution
setopt EXTENDED_HISTORY # record command start time

# EMACS keymap in ZLE
bindkey -e
# Fix the word navigation
WORDCHARS=''

# Initialise completion
autoload -Uz compinit
compinit

# Version control prompt status configuration
autoload -Uz add-zsh-hook
add-zsh-hook precmd vcs_info
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' use-prompt-escapes true
zstyle ':vcs_info:*' stagedstr '%F{yellow}s|%f'
zstyle ':vcs_info:*' unstagedstr '%F{yellow}u|%f'
zstyle ':vcs_info:*' formats       '%u%c%K{white}%F{black}%b%f%k'
zstyle ':vcs_info:*' actionformats '%u%c%K{white}%F{black}%b%f%k>%a'

# History
export HISTFILE=~/.zhistory
export HISTSIZE=1000000 # the maximum number of events stored in the internal history list
export SAVEHIST=1000000 # the maximum number of history events to save in the history file.

# `time` formatting
export TIMEFMT="%J | CPU %U user, %S system, %*E total, %P | MEM %MkB"

# reset the prompt
PROMPT=""
# indicate SSH connection
if [[ "${SSH_TTY}" ]]; then
		PROMPT+='%B%F{yellow}ssh://%f%b'
fi;
# username and hostname
PROMPT+='%B%F{magenta}%n@%M%f%b'
# working directory
PROMPT+='%F{white}:%~%f'
# amount of running background jobs on the machine
PROMPT+='%(1j. [%j].)'
# red if the last exit code was non-zero
PROMPT+=' %(?.%F{green}%#.%F{red}%#|%?|)%f '
# git branch and status flags
RPROMPT='${vcs_info_msg_0_}'
ZLE_RPROMPT_INDENT=0

# Load machine-specific environment
if [ -f ~/.zshrc.local ]; then
	source ~/.zshrc.local;
fi

# Load aliases
if [ -f ~/.aliases ]; then
	source ~/.aliases
fi

[[ -s "/Users/rdner/.gvm/scripts/gvm" ]] && source "/Users/rdner/.gvm/scripts/gvm"
